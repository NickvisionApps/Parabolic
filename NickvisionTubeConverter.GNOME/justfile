app_id := "org.nickvision.tubeconverter"
project_name := "NickvisionTubeConverter"
lib := "lib" # this is added to prefix, see "publish" recipe for details

default:
    @just --list

# Run the application without installing
run:
    blueprint-compiler batch-compile Blueprints/ Blueprints/ Blueprints/*.blp
    dotnet build
    rm Blueprints/*.ui
    glib-compile-resources --sourcedir ./Resources \
    ./Resources/{{app_id}}.gresource.xml
    mv ./Resources/{{app_id}}.gresource ./bin/Debug/net7.0/
    dotnet run

# Install the app
publish PREFIX LIB_DIR=lib: && (_install_extras PREFIX)
    #!/bin/sh
    # LIB_DIR id added to prefix.
    # For example, for PREFIX "/usr" and LIB_DIR "lib"
    # all dll and so files will go to "/usr/lib".
    # Resources will go to {{PREFIX}}/shared.

    ARCH={{arch()}}
    if [ "$ARCH" == "x86_64" ]
    then
        RUNTIME="linux-x64"
    elif [ "$ARCH" == "arm" ]
    then
        RUNTIME="linux-arm"
    elif [ "$ARCH" == "aarch64" ]
    then
        RUNTIME="linux-arm64"
    fi

    blueprint-compiler batch-compile Blueprints/ Blueprints/ Blueprints/*.blp
    mkdir -p {{PREFIX}}/{{LIB_DIR}}/{{app_id}}
    dotnet publish -c Release {{project_name}}.GNOME.csproj \
        $([ "$RUNTIME" ] && echo "--runtime $RUNTIME") \
        --self-contained false -o {{PREFIX}}/{{LIB_DIR}}/{{app_id}}
    rm Blueprints/*.ui
    mkdir -p {{PREFIX}}/bin
    cat > {{PREFIX}}/bin/{{app_id}} << EOF
    #!/bin/sh
    exec dotnet {{PREFIX}}/{{LIB_DIR}}/{{app_id}}/{{project_name}}.GNOME.dll
    EOF
    chmod +x {{PREFIX}}/bin/{{app_id}}

# Install the app (self-contained)
publish-self-contained PREFIX LIB_DIR=lib: && (_install_extras PREFIX)
    #!/bin/sh
    # BIN_DIR and LIB_DIR are added to prefix
    # For example, for PREFIX "/usr" and BIN_DIR "/bin" the executable will go to "/usr/bin"
    # All dll and so files will go to {{PREFIX}}/{{LIB_DIR}} (e.g. "/usr/lib")
    # Resources will go to {{PREFIX}}/shared

    ARCH={{arch()}}
    if [ "$ARCH" == "x86_64" ]
    then
        RUNTIME="linux-x64"
    elif [ "$ARCH" == "arm" ]
    then
        RUNTIME="linux-arm"
    elif [ "$ARCH" == "aarch64" ]
    then
        RUNTIME="linux-arm64"
    fi

    blueprint-compiler batch-compile Blueprints/ Blueprints/ Blueprints/*.blp
    mkdir -p {{PREFIX}}/{{LIB_DIR}}/{{app_id}}
    dotnet publish -c Release {{project_name}}.GNOME.csproj \
        $([ "$RUNTIME" ] && echo "--runtime $RUNTIME") \
        --self-contained true -o {{PREFIX}}/{{LIB_DIR}}/{{app_id}}
    rm Blueprints/*.ui
    mkdir -p {{PREFIX}}/bin
    cat > {{PREFIX}}/bin/{{app_id}} << EOF
    #!/bin/sh
    exec {{PREFIX}}/{{LIB_DIR}}/{{app_id}}/{{project_name}}.GNOME
    EOF
    chmod +x {{PREFIX}}/bin/{{app_id}}

# Command to be used in flatpak manifest
publish-flatpak: && (_install_extras "/app")
    #!/bin/sh
    ARCH={{arch()}}
    if [ "$ARCH" == "x86_64" ]
    then
        RUNTIME="linux-x64"
    elif [ "$ARCH" == "arm" ]
    then
        RUNTIME="linux-arm"
    elif [ "$ARCH" == "aarch64" ]
    then
        RUNTIME="linux-arm64"
    fi

    blueprint-compiler batch-compile Blueprints/ Blueprints/ Blueprints/*.blp
    mkdir -p /app/lib/{{app_id}}
    dotnet publish -c Release {{project_name}}.GNOME.csproj \
        $([ -d "$FLATPAK_BUILDER_BUILDDIR/nuget-sources" ] && \
        echo "--source $FLATPAK_BUILDER_BUILDDIR/nuget-sources") \
        --runtime $RUNTIME --self-contained true -o /app/lib/{{app_id}}
    rm Blueprints/*.ui
    mkdir -p /app/bin
    cat > /app/bin/{{app_id}} << EOF
    #!/bin/sh
    exec /app/lib/{{app_id}}/{{project_name}}.GNOME
    EOF
    chmod +x /app/bin/{{app_id}}

_install_extras PREFIX: && (_translate_meta PREFIX)
    # Installing icons
    mkdir -p {{PREFIX}}/share/icons/hicolor/scalable/apps
    cp ../{{project_name}}.Shared/Resources/{{app_id}}.svg \
        {{PREFIX}}/share/icons/hicolor/scalable/apps/
    cp ../{{project_name}}.Shared/Resources/{{app_id}}-devel.svg \
        {{PREFIX}}/share/icons/hicolor/scalable/apps/
    mkdir -p {{PREFIX}}/share/icons/hicolor/symbolic/apps
    cp ../{{project_name}}.Shared/Resources/{{app_id}}-symbolic.svg \
        {{PREFIX}}/share/icons/hicolor/symbolic/apps/

    # Installing GResource
    mkdir -p {{PREFIX}}/share/{{app_id}}
    glib-compile-resources --sourcedir ./Resources ./Resources/{{app_id}}.gresource.xml
    mv ./Resources/{{app_id}}.gresource \
        {{PREFIX}}/share/{{app_id}}/

    # Installing desktop file
    mkdir -p {{PREFIX}}/share/applications
    cp ./{{app_id}}.desktop {{PREFIX}}/share/applications/
    desktop-file-edit --set-key='Exec' \
    --set-value='{{PREFIX}}/bin/{{app_id}}' \
    {{PREFIX}}/share/applications/{{app_id}}.desktop

    # Installing metainfo
    mkdir -p {{PREFIX}}/share/metainfo
    cp ./{{app_id}}.metainfo.xml {{PREFIX}}/share/metainfo/

_translate_meta PREFIX:
    #!/usr/bin/env python3
    import os
    import re
    import xml.etree.ElementTree as ET
    from pathlib import Path

    print("Translating desktop and metainfo files")
    resx_dir = (Path(os.getcwd()) / '../{{project_name}}.Shared/Resources/').resolve()
    regex = re.compile(r'Strings\.(.+)\.resx')
    desktop_comments = []
    desktop_keywords = []
    meta_summaries = []
    meta_descriptions = []
    for filename in os.listdir(resx_dir):
        regex_match = regex.search(filename)
        if regex_match:
            lang_code = regex_match.group(1)
            tree = ET.parse(f'{resx_dir}/{filename}')
            root = tree.getroot()
            for item in root.findall('./data'):
                if item.attrib['name'] == 'Description':
                    text = item.find('value').text
                    if text:
                        desktop_comments.append(f'Comment[{lang_code}]={text}')
                        meta_descriptions.append(f'    <p xml:lang="{lang_code}">\n      {text}\n    </p>')
                elif item.attrib['name'] == 'Summary.GTK':
                    text = item.find('value').text
                    if text:
                        meta_summaries.append(f'  <summary xml:lang="{lang_code}">{text}</summary>')
                elif item.attrib['name'] == 'Keywords.GTK':
                    text = item.find('value').text
                    if text:
                        desktop_keywords.append(f'Keywords[{lang_code}]={text}')
    desktop_comments.sort()
    desktop_keywords.sort()
    meta_summaries.sort()
    meta_descriptions.sort()

    with open('{{PREFIX}}/share/applications/{{app_id}}.desktop', 'r') as f:
        contents = f.readlines()
        new_contents = contents.copy()
    j = 0
    for i in range(len(contents)):
        if contents[i].startswith('Comment='):
            new_contents.insert(j + 1, "\n".join(desktop_comments) + "\n")
            j += 1
        elif contents[i].startswith('Keywords='):
            new_contents.insert(j + 1, "\n".join(desktop_keywords) + "\n")
            j += 1
        j += 1
    with open('{{PREFIX}}/share/applications/{{app_id}}.desktop', 'w') as f:
        new_contents = "".join(new_contents)
        f.write(new_contents)

    with open('{{PREFIX}}/share/metainfo/{{app_id}}.metainfo.xml', 'r') as f:
        contents = f.readlines()
        new_contents = contents.copy()
    j = 0
    for i in range(len(contents)):
        if contents[i].find('<summary>') > -1:
            new_contents.insert(j + 1, "\n".join(meta_summaries) + "\n")
            j += 1
        elif contents[i].find('<description>') > -1:
            new_contents.insert(j + 4, "\n".join(meta_descriptions) + "\n")
            break
        j += 1
    with open('{{PREFIX}}/share/metainfo/{{app_id}}.metainfo.xml', 'w') as f:
        new_contents = "".join(new_contents)
        f.write(new_contents)
    print("Done!")

# Generate sources files for Flatpak
generate-flatpak-sources: _generate-flatpak-sources-just _generate-flatpak-sources-dotnet

_generate-flatpak-sources-just:
    # Generating sources for just
    curl "https://raw.githubusercontent.com/casey/just/baa2dfcc6f180d123672544c5ed1aedc32608228/Cargo.lock" -o /tmp/Cargo.lock
    flatpak-cargo-generator.py -o cargo-sources.json /tmp/Cargo.lock

_generate-flatpak-sources-dotnet:
    # Generating sources for dotnet
    flatpak-dotnet-generator.py nuget-sources.json {{project_name}}.GNOME.csproj